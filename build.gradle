plugins {
  id 'groovy'
  id 'maven-publish'
  id 'org.standardout.eclipseconfig' version '1.1.0'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'to.wetransform'
version = '0.6.0-SNAPSHOT'

repositories {
  jcenter()
  maven {
    url 'https://jitpack.io'
  }
}

dependencies{
  compile gradleApi()
  compile localGroovy()
  
  // template engine
  // https://github.com/PebbleTemplates/pebble
  compile 'io.pebbletemplates:pebble:3.1.5'
  
  // YAML processing
  compile 'org.yaml:snakeyaml:1.18'
  
  // Docker plugin
  compile 'com.bmuschko:gradle-docker-plugin:3.1.0'
  
  // Encryption library
  compile 'com.github.rockaport:alice:0.8.0', {
    exclude group: 'org.codehaus.groovy', module: 'groovy-all'
  }

  // testing
  testCompile 'junit:junit:4.12'
}

/*
 * Some general project configuration
 */
configurations.all {
  // ensure SNAPSHOTs are updated every time if needed
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.1'
}

/*
 * Publishing to Maven
 *
 * https://docs.gradle.org/current/userguide/publishing_maven.html
 */
publishing {
  publications {
    plugin(MavenPublication) {
      from components.java
    }
  }

  repositories {
    maven {
      url 'https://artifactory.wetransform.to/artifactory/' +
        (version.endsWith('-SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local')
      credentials {
        username project.hasProperty('wetfArtifactoryUser') ? wetfArtifactoryUser : ''
        password project.hasProperty('wetfArtifactoryPassword') ? wetfArtifactoryPassword : ''
      }
    }
  }
}
