plugins {
  id 'groovy'
  id 'maven-publish'
  id 'to.wetransform.conventions' version '1.6.0'
}

repositories {
  mavenCentral()
  gradlePluginPortal()
}

wetransform {
  repos {
    wetfPublic()
  }

  setup {
    javaVersion(8)

    spotless {
      licenseHeader = '''\
      /*
       * Copyright $YEAR wetransform GmbH
       *
       * Licensed under the Apache License, Version 2.0 (the "License");
       * you may not use this file except in compliance with the License.
       * You may obtain a copy of the License at
       *
       *     http://www.apache.org/licenses/LICENSE-2.0
       *
       * Unless required by applicable law or agreed to in writing, software
       * distributed under the License is distributed on an "AS IS" BASIS,
       * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
       * See the License for the specific language governing permissions and
       * limitations under the License.
       */
      '''.stripIndent()
    }

    publish {
      // publish to public repo
      privateRepo = false
    }
  }
}

dependencies{
  implementation gradleApi()
  implementation localGroovy()

  // template engine
  // https://github.com/PebbleTemplates/pebble
  implementation libs.pebble

  implementation libs.semver4j

  // YAML processing
  implementation libs.snakeyaml

  // Docker plugin
  implementation libs.gradle.docker.plugin

  // Encryption library
  implementation libs.alice, {
    exclude group: 'org.codehaus.groovy', module: 'groovy'
  }

  // testing
  testImplementation testLibs.junit
}

tasks.wrapper {
  distributionType = Wrapper.DistributionType.ALL
  gradleVersion = '8.14'
}
