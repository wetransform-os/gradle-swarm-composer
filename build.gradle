plugins {
  id 'groovy'
  id 'maven-publish'
  id 'org.standardout.eclipseconfig' version '1.1.0'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(8)
  }
}

group = 'to.wetransform'
version = '1.1.0-SNAPSHOT'

repositories {
  mavenCentral()
  maven {
    url 'https://jitpack.io'
  }
}

dependencies{
  implementation gradleApi()
  implementation localGroovy()

  // template engine
  // https://github.com/PebbleTemplates/pebble
  implementation 'io.pebbletemplates:pebble:3.1.5'

  implementation 'com.vdurmont:semver4j:3.1.0'

  // YAML processing
  implementation 'org.yaml:snakeyaml:2.2'

  // Docker plugin
  implementation 'com.bmuschko:gradle-docker-plugin:6.7.0'

  // Encryption library
  implementation 'com.github.rockaport:alice:0.8.0', {
    exclude group: 'org.codehaus.groovy', module: 'groovy-all'
  }

  // testing
  testImplementation 'junit:junit:4.12'
}

/*
 * Some general project configuration
 */
configurations.all {
  // ensure SNAPSHOTs are updated every time if needed
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

configurations {
  // activate dependency locks for lockfile generation (used by trivy security scan)
  compileClasspath {
    resolutionStrategy.activateDependencyLocking()
  }
  runtimeClasspath {
    resolutionStrategy.activateDependencyLocking()
  }
}

tasks.wrapper {
  distributionType = Wrapper.DistributionType.ALL
  gradleVersion = '8.4'
}

/*
 * Publishing to Maven
 *
 * https://docs.gradle.org/current/userguide/publishing_maven.html
 */
publishing {
  publications {
    plugin(MavenPublication) {
      from components.java
    }
  }

  repositories {
    maven {
      url 'https://artifactory.wetransform.to/artifactory/' +
        (version.endsWith('-SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local')
      credentials {
        username project.hasProperty('wetfArtifactoryUser') ? wetfArtifactoryUser : ''
        password project.hasProperty('wetfArtifactoryPassword') ? wetfArtifactoryPassword : ''
      }
    }
  }
}
